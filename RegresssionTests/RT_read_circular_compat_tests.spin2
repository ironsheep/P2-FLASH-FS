'' =================================================================================================
''
''   File....... RT_read_circular_compat_tests.spin2
''   Purpose.... This object exercises the open_circular() and read/write to circular files of the Flash file system
''   Author..... Stephen M Moraco
''               -- see below for terms of use
''   E-mail.....stephen@ironsheep.biz
''   Started.... DEC 2023
''   Updated.... 24 DEC 2023
''
'' =================================================================================================

CON

     _CLKFREQ        = 320_000_000

'{
    ' debug via serial to RPi (using RPi gateway pins)
    '  USES LED1 (56) and LED2 (57) pins
    DEBUG_PIN_TX = 57           ' GRY GPIO 10
    DEBUG_PIN_RX = 56           ' WHT GPIO 8 (gnd is BLK GPIO 6)
    DEBUG_WINDOWS_OFF = -1      ' no debug windows showing
    DEBUG_BAUD = 2_000_000      ' comms at 2Mbit
'}

OBJ
    flash : "flash_fs"
    utils : "RT_utilities"

CON
    RECORD_LEN = 1024  ' just for fun, tiny but even number of longs
    RECORD_DATA_LEN = RECORD_LEN - 12

    CIRCULAR_LEN_IN_BYTES = RECORD_LEN * 8              ' 8_192 bytes


DAT

testfileA1      BYTE    "rw0KB-A1-blocks.bin", 0
testfileA2      BYTE    "rw0KB-A2-blocks.bin", 0
testfileA3      BYTE    "rw0KB-A3-blocks.bin", 0
testfileB1      BYTE    "rw3KB-B1-blocks.bin", 0
testfileB2      BYTE    "rw3KB-B2-blocks.bin", 0
testfileB3      BYTE    "rw3KB-B3-blocks.bin", 0
testfileB3a      BYTE   "rw3KB-B3a-blocks.bin", 0
testfileB4      BYTE    "rw3KB-B4-blocks.bin", 0
testfileB5      BYTE    "rw3KB-B5-blocks.bin", 0
testfileC1      BYTE    "rw8KB-C1-blocks.bin", 0
testfileC2      BYTE    "rw8KB-C2-blocks.bin", 0
'testfileC3      BYTE    "rw8KB-C3-blocks.bin", 0
'testfileD1      BYTE    "rw9KB-D1-blocks.bin", 0
testfileD2      BYTE    "rw9KB-D2-blocks.bin", 0

record
recordMARK      LONG    $beadfeed              ' visual start of record marker
recordID        LONG    0
recordData      LONG    0[RECORD_DATA_LEN/4]
recordCRC       LONG    0
recordEnd

status          LONG    0
bValue          LONG    0
handle          LONG    0
testValue       LONG    0
recordNbr       LONG    0
rdLength        LONG    0
pBlock          LONG    0
calcCRC         LONG    0
sizeUnused      LONG    0
endPosition     LONG    0
writeLength     LONG    0

expectedFileSize    LONG    0
expectedFileBlocks  LONG    0
allocatedFileSize   LONG    0

PUB go()

    utils.startTest(@"Check Flash Mount-ability")
    utils.setCheckCountPerTest(2)
    bValue := flash.can_mount()      ' Comment out this line to not erase all files
    status := flash.error()
    utils.evaluateSubValue(bValue, @"can_mount()", TRUE)
    utils.evaluateSubStatus(status, @"can_mount()", flash.SUCCESS)

    utils.showMountSignatures()

    'utils.startTest(@"Format Flash")
    'status := flash.format()      ' Comment out this line to not erase all files
    'utils.evaluateResultNegError(handle, flash.SUCCESS)

    utils.startTest(@"Mount Flash FS")
    status := flash.mount()
    utils.evaluateResultNegError(handle, flash.SUCCESS)

    utils.ShowStats()                                   ' should have empty filesystem
    utils.showFiles()

    utils.showChipAndDriverVersion()

    ' Test Cases:
    '  Limit 8kb
    '   A - Cases with NO starting file (0kb)
    '     A1 - write less than 8kb                      - close as file -> end with file less than limit
    '     A2 - write 8kb                                - close as file -> end with file at limit
    '     A3 - write more than 8kb (9kb)                - (move head, canceling prior head and block(s)) moves head into chain, lops off head part of chain -> end with file at limit
    '   B - Cases with SHORTer starting file (3kb)
    '     B1 - write amount that will still fit in 8kb  - close as file -> end with file at limit (tail gets replaced) -> end with file less than limit
    '     B2 - write amount that will fill 8kb          - close as file -> end with file at limit (tail gets replaced) -> end with file at limit
    '     B3 - write 6k                                 - move head into file, append tail to file (removed front block(s) of orig file -> end with file at limit
    '     B4 - write 8kb                                - moved head of file to head of chain, remove orig file -> end with file at limit
    '     B5 - write more than 8kb (9kb)                - move head into middle of chain, (remove orig file, remove from of chain prior to head) -> end with file at limit
    '   C - Cases with 8kb starting file (8kb)
    '     C1 - write 6k                                 - move head into file, append tail to file (removed front block(s) of orig file) -> end with file at limit
    '     C2 - write more than 8kb (9kb)                - move head into middle of chain (remove orig file, remove from of chain prior to head) -> end with file at limit
    '   D - Cases with LONGer starting file (11kb)
    '     D1 - write 6k                                 - move head into file, append tail to file (removed front block(s) of orig file) -> end with file at limit
    '     D2 - write more than 8kb (9kb)                - move head into middle of chain, (remove orig file, remove from of chain prior to head) -> end with file at limit

    ' Test Cases:
    '  Limit 8kb - Allocates blocks which can handle 11KB before overflowing (12,132 technically)

    '   A - Cases with NO starting file (0kb)
    '     A1 - write less than 8kb                      - close as file -> end with file less than limit
    utils.startTestGroup(@"A1 Append 6KB circular to empty file")
    'testAppendRecords(@testfileA1, 6, 0, 0)             ' this creates file (6,144 = 2 blocks)
    testVerifyRecords(@testfileA1, 6)                   ' this verifies all of file

    utils.showFiles()

    '     A2 - write 8kb                                - close as file -> end with file at limit
    utils.startTestGroup(@"A2 Append 8KB circular to empty file")
    'testAppendRecords(@testfileA2, 8, 1, 2)             ' this creates file (8,192 = 3 blocks)
    testVerifyRecords(@testfileA2, 8)                   ' this verifies all of file

    utils.showFiles()

    '     A3 - write more than 8kb (12kb)               - (move head, canceling prior head and block(s)) moves head into chain, lops off head part of chain -> end with file at limit
    utils.startTestGroup(@"A3 Append 12KB circular to empty file")
    'testAppendRecords(@testfileA3, 12, 2, 5)            ' this creates file (12,132 = 4 blocks, froncated to 3 blocks)
    testVerifyRecords(@testfileA3, 12)                  ' this verifies last 8k of file

    utils.showFiles()

    '   B - Cases with SHORTer starting file (3kb)
    '     B1 - write amount that will still fit in 8kb  - close as file -> end with file at limit (tail gets replaced) -> end with file less than limit
    utils.startTestGroup(@"B1 Append 5KB circular to 3kb file")
    'testAppendRecords(@testfileB1, 3, 3, 8) ' this creates file (3,072 = 1 block)
    'testAppendRecords(@testfileB1, 5, 4, 9) ' this appends to file (8,192 = 3 blocks)
    testVerifyRecords(@testfileB1, 8) ' this verifies last 8k of file

    utils.showFiles()

    '     B2 - write amount that will fill 8kb          - close as file -> end with file at limit (tail gets replaced) -> end with file at limit
    utils.startTestGroup(@"B2 Append 8KB circular to 3kb file")
    'testAppendRecords(@testfileB2, 3, 4, 11) ' this creates file (3,072 = 1 block)
    'testAppendRecords(@testfileB2, 8, 5, 12) ' this appends to file (11,264 = 3 blocks)
    testVerifyRecords(@testfileB2, 11) ' this verifies last 8k of file

    utils.showFiles()

    '     B3 - write 8kb                                - moved head of file to head of chain, remove orig file -> end with file at limit
    utils.startTestGroup(@"B3 Append 11KB circular to 3kb file")
    'testAppendRecords(@testfileB3, 3, 5, 14) ' this creates file (3,072 = 1 block)
    'testAppendRecords(@testfileB3, 11, 6, 15) ' this appends to file (truncates to 12,132 last 8,192 valid = 3 blocks)
    testVerifyRecords(@testfileB3, 14) ' this verifies last 8k of file

    utils.startTestGroup(@"B3a Seek within file that was append 11KB circular to 3kb file")
    testCopyFile(@testfileB3, @testfileB3a)
    testVerifySeekRecords(@testfileB3a, 14) ' this verifies seeks within last 8k of file

    utils.showFiles()

    '     B4 - write more than 8kb (9kb)                - move head into middle of chain, (remove orig file, remove from of chain prior to head) -> end with file at limit
    utils.startTestGroup(@"B4 Append 14KB circular to 3kb file")
    'testAppendRecords(@testfileB4, 3, 6, 17) ' this creates file (3,072 = 1 block)
    'testAppendRecords(@testfileB4, 14, 7, 18) ' this appends to file (truncates to 12,132 last 8,192 valid = 3 blocks)
    testVerifyRecords(@testfileB4, 17) ' this verifies last 8k of file

    utils.showFiles()

    '   C - Cases with 8kb starting file (multi-block file)
    '     C1 - write more than 8kb (3kb)                - move head into file, (remove orig file, remove from of chain prior to head) -> end with file at limit
    utils.startTestGroup(@"C1 Append 4KB circular to 8kb file")
    'testAppendRecords(@testfileC1, 8, 7, 20) ' this creates file (8,192 = 3 blocks)
    'testAppendRecords(@testfileC1, 4, 8, 23) ' this appends to file (truncates to 12,132 last 8,192 valid = 3 blocks)
    testVerifyRecords(@testfileC1, 12) ' this verifies last 8k of file

    utils.showFiles()

    '     C2 - write more than 8kb (6kb)                - move head into file, append tail to file (removed front block(s) of orig file) -> end with file at limit
    utils.startTestGroup(@"C2 Append 6KB circular to 8kb file")
    'testAppendRecords(@testfileC2, 8, 8, 23) ' this creates file (8,192 = 3 blocks)
    'testAppendRecords(@testfileC2, 6, 9, 26) ' this appends to file (truncates to 12,132 last 8,192 valid = 3 blocks)
    testVerifyRecords(@testfileC2, 14) ' this verifies last 8k of file

    utils.showFiles()

    '     C3 - write more than 8kb (9kb)                - move head into middle of chain (remove orig file, remove from of chain prior to head) -> end with file at limit
    'utils.startTestGroup(@"C3 Append 9KB circular to 8kb file")
    'testAppendRecords(@testfileC3, 8, 9, 26) ' this creates file (8,192 = 3 blocks)
    'testAppendRecords(@testfileC3, 9, 10, 29) ' this appends to file (truncates to 12,132 last 8,192 valid = 3 blocks)
    'testVerifyRecords(@testfileC3, 17) ' this verifies last 8k of file

    'utils.showFiles()

    '   D - Cases with LONGer starting file (11kb)
    '     D1 - write 6k                                 - move head into file, append tail to file (removed front block(s) of orig file) -> end with file at limit
    'utils.startTestGroup(@"D1 Append 6KB circular to 11kb file")
    'testAppendRecords(@testfileD1, 11, 10, 29) ' this creates file (11,264 = 3 blocks)
    'testAppendRecords(@testfileD1, 6, 11, 32) ' this appends to file (truncates to 12,132 last 8,192 valid = 3 blocks)
    'testVerifyRecords(@testfileD1, 17) ' this verifies last 8k of file

    'utils.showFiles()

    '     D2 - write more than 8kb (9kb)                - move head into middle of chain, (remove orig file, remove from of chain prior to head) -> end with file at limit
    utils.startTestGroup(@"D2 Append 9KB circular to 11kb file")
    'testAppendRecords(@testfileD2, 11, 11, 32) ' this creates file (11,264 = 3 blocks)
    'testAppendRecords(@testfileD2, 9, 12, 35) ' this appends to file (truncates to 12,132 last 8,192 valid = 3 blocks)
    testVerifyRecords(@testfileD2, 20) ' this verifies last 8k of file

    utils.showFiles()

    utils.startTest(@"Unmount Flash")
    status := flash.unmount()      ' Comment out this line to not erase all files
    utils.evaluateResultNegError(handle, flash.SUCCESS)

    utils.ShowTestEndCounts()

    debug("* Test Complete")


DAT ' verifyMessages

readMsg       BYTE  "Read() Rcd #"
readNbr       BYTE  "xx",0
idMsg         BYTE  "recordID Rcd #"
idNbr         BYTE  "xx",0
crcMsg        BYTE  "recordCRC Rcd #"
crcNbr        BYTE  "xx",0

PRI setMsgRcdNbr(rcdNumber) | units, tens
  tens := (rcdNumber / 10) + $30
  units := (rcdNumber +// 10) + $30
  if(tens == $30)
    ' no leading zero
    BYTE[@readNbr][0] := units
    BYTE[@readNbr][1] := 0
    BYTE[@idNbr][0] := units
    BYTE[@idNbr][1] := 0
    BYTE[@crcNbr][0] := units
    BYTE[@crcNbr][1] := 0
  else
    BYTE[@readNbr][0] := tens
    BYTE[@readNbr][1] := units
    BYTE[@idNbr][0] := tens
    BYTE[@idNbr][1] := units
    BYTE[@crcNbr][0] := tens
    BYTE[@crcNbr][1] := units


PUB testCopyFile(pOldFilename, pNewFilename) | rdHandle, wrHandle, rdStatus, wrStatus, wrLength, totalWritten, totalRead
'' copy existing file to new name
''
'' @param pOldFilename - address of zstring with existing file name
'' @param pNewFilename - address of zstring with new file name

' Local Variables:
' @local rdHandle -
' @local wrHandle -
' @local rdStatus -
' @local wrStatus -
' @local wrLength -

    utils.startTestGroup(@"Copy file")
    utils.showFileChain(pOldFilename)

    utils.startTest(@"Ensure old exists")
    status := flash.exists(pOldFilename)
    utils.evaluateSingleValue(status, @"exists()", true)

    ' remove existing file if it exists
    if flash.exists(pNewFilename)
      utils.startTest(@"Delete our existing write file")
      status := flash.delete(pNewFilename)
      utils.evaluateResultNegError(status, flash.SUCCESS)

    utils.startTest(@"Ensure new doesn't exist")
    status := flash.exists(pNewFilename)
    utils.evaluateSingleValue(status, @"exists()", false)

    utils.startTest(@"Open File for read")
    rdHandle := flash.open(pOldFilename, "r")
    utils.evaluateResultNegError(rdHandle, 0)

    utils.startTest(@"Open File for write")
    wrHandle := flash.open_circular(pNewFilename, "a", CIRCULAR_LEN_IN_BYTES)
    utils.evaluateResultNegError(wrHandle, 1)

    utils.startTest(@"copy contents")
    utils.setCheckCountPerTest(4)
    repeat
      rdLength := flash.read(rdHandle, @record, RECORD_LEN)
      totalRead += rdLength
      rdStatus := flash.error()
      if(rdStatus == flash.E_END_OF_FILE)
        ' allow short record on EOF
        utils.evaluateSubValue(rdLength, @"read()", rdLength)
        ' EOF is legal status
        utils.evaluateSubStatus(rdStatus, @"read()", flash.E_END_OF_FILE)
      else
        ' full record if NOT EOF
        utils.evaluateSubValue(rdLength, @"read()", RECORD_LEN)
        ' need success if NOT EOF
        utils.evaluateSubStatus(rdStatus, @"read()", flash.SUCCESS)
      if rdStatus <> flash.SUCCESS and rdStatus <> flash.E_END_OF_FILE
        quit

      wrLength := flash.write(wrHandle, @record, rdLength)
      totalWritten += wrLength
      utils.evaluateSubValue(wrLength, @"write()", rdLength)
      wrStatus := flash.error()
      utils.evaluateSubStatus(wrStatus, @"write()", flash.SUCCESS)
      if wrStatus <> flash.SUCCESS
        quit

      if rdStatus == flash.E_END_OF_FILE
        quit
    while wrStatus == flash.SUCCESS

    debug("* copy result ", udec(totalRead, totalWritten))

    utils.startTest(@"ensure no errors on reads")
    utils.evaluateResultNegError(rdStatus, flash.E_END_OF_FILE)

    utils.startTest(@"ensure no errors on writes")
    utils.evaluateResultNegError(wrStatus, flash.SUCCESS)

    utils.startTest(@"Close read file")
    status := flash.close(rdHandle)
    utils.evaluateResultNegError(status, flash.SUCCESS)

    utils.startTest(@"Close write file")
    status := flash.close(wrHandle)
    utils.evaluateResultNegError(status, flash.SUCCESS)

    utils.showFileChain(pNewFilename)


PRI testVerifyRecords(pFilename, totalRecordsWritten) | recordCount, lenBytes, startRcdNbr, recordInCircular

    '  get total file size
    utils.startTest(@"Determine size of file in records")
    lenBytes := flash.file_size(pFilename)
    status := flash.error()
    utils.evaluateResultNegError(handle, flash.SUCCESS)

    recordCount := lenBytes / RECORD_LEN                ' ignore fraction at front of file

    debug("  ")
    if lenBytes > 0
      debug(" ------------------ ")
    debug("ENTRY testVerifyRecords(): file size ", udec(lenBytes, recordCount, totalRecordsWritten))

    recordInCircular := (CIRCULAR_LEN_IN_BYTES + (RECORD_LEN-1)) / RECORD_LEN
    debug("   (Circular Buffer limit is ", udec_(recordInCircular), " records)")

    ' read should start at tail - file limit (8KB in our tests)
    if totalRecordsWritten > recordInCircular
        startRcdNbr := totalRecordsWritten - recordInCircular + 1
    else
        startRcdNbr := 1
    debug("   Read should start with record #", udec_(startRcdNbr))

    '  open file for read
    utils.startTest(@"Open File for read")
    handle := flash.open_circular(pFilename, "r", CIRCULAR_LEN_IN_BYTES)
    utils.evaluateResultNegError(handle, flash.SUCCESS)
    '    seek to various locations and read longs validating for correct value

    utils.startTest(@"read expected RECORDs")
    utils.setCheckCountPerTest(3)
    repeat recordNbr from startRcdNbr to totalRecordsWritten
        setMsgRcdNbr(recordNbr)
        rdLength := flash.read(handle, @record, RECORD_LEN)
        ' ensure length is same as expected
        utils.evaluateSubValue(rdLength, @readMsg, RECORD_LEN)
        if rdLength <> RECORD_LEN
            status := flash.error()
            quit
        ' ensure record ID is same as expected
        utils.evaluateSubValue(recordID, @idMsg, recordNbr)
        ' ensure record CRC is same as expected
        calcCRC := utils.record_crc(@record, RECORD_LEN)
        utils.evaluateSubValue(calcCRC, @crcMsg, recordCRC)
        ' IF WE HAVE a CRC error then show buffers!
        if calcCRC <> recordCRC
          dumpBlocksAtSplice(pFilename, recordNbr - startRcdNbr, totalRecordsWritten, @record, RECORD_LEN)

    utils.startTest(@"ensure no errors on reads")
    utils.evaluateResultNegError(status, flash.SUCCESS)

    '  read past end of file to verify read returns error
    utils.startTest(@"BAD: read one more Record than written")
    utils.setCheckCountPerTest(2)
    rdLength := flash.read(handle, @record, RECORD_LEN)
    utils.evaluateSubValue(rdLength, @"read()", 0)
    status := flash.error()
    utils.evaluateSubStatus(status, @"read()", flash.E_END_OF_FILE)

    '  close file
    utils.startTest(@"Close read file")
    status := flash.close(handle)
    utils.evaluateResultNegError(status, flash.SUCCESS)



PRI dumpBlocksAtSplice(pFilename, rcdNbr, totalRecordsWritten, pRcdBuffer, rcdSize) | fileOffset, p_buffer, p_buffer2, blkAddr, blkAddr2

    fileOffset := rcdNbr * rcdSize
    p_buffer, p_buffer2, blkAddr, blkAddr2  := flash.TEST_get4kBlockAndNextAtOffset(pFilename, fileOffset)

    if p_buffer
      ' dump record
      utils.dbgMemDump(@"Record HEAD", pRcdBuffer, 128)
      utils.dbgMemDump(@"Record TAIL", @BYTE[pRcdBuffer][rcdSize-128], 128)
      debug("  ")
      ' dump first block tail
      debug("/* [", uhex_word_(blkAddr),"]")
      utils.dbgMemDump(@"lead-buffer TAIL", @BYTE[p_buffer][4096-256], 256)
      ' dump second block head
      if p_buffer2
          debug("  ")
          debug("/* [", uhex_word_(blkAddr2),"]")
          utils.dbgMemDump(@"trail-buffer HEAD", p_buffer2, 256)
      else
          debug("- no following block")
    else
        debug("- dumpBlocksAtSplice() FILE NOT FOUND [", zstr_(pFilename), "]")


PRI testVerifySeekRecords(pFilename, totalRecordsWritten) | recordCount, lenBytes, startRcdNbr, recordInCircular

    utils.startTestGroup(@"Exercise seeks in new format circular file")
    utils.showFileChain(pFilename)

    '  get total file size
    utils.startTest(@"Determine size of file in records")
    lenBytes := flash.file_size(pFilename)
    status := flash.error()
    utils.evaluateResultNegError(handle, flash.SUCCESS)

    recordCount := lenBytes / RECORD_LEN                ' ignore fraction at front of file

    debug(" ")
    debug("****  At VerifySeek: file size in bytes = ", udec_(lenBytes), " bytes, (", udec_(recordCount), " records), wrote total of ", udec_(totalRecordsWritten), " records")

    recordInCircular := (CIRCULAR_LEN_IN_BYTES + (RECORD_LEN-1)) / RECORD_LEN
    debug("   Circular Buffer contains = ", udec_(recordInCircular), " records")

    ' read should start at tail - file limit (8KB in our tests)
    if totalRecordsWritten > recordInCircular
        startRcdNbr := totalRecordsWritten - recordInCircular + 1
    else
        startRcdNbr := 1
    debug("   Read should start with record #", udec_(startRcdNbr))

    '  open file for read
    utils.startTest(@"Open File for read")
    handle := flash.open_circular(pFilename, "r", CIRCULAR_LEN_IN_BYTES)
    utils.evaluateResultNegError(handle, flash.SUCCESS)
    '    seek to various locations and read longs validating for correct value

    ' seek to 3
    utils.startTest(@"seek to 3 then read expected RECORD")
    utils.setCheckCountPerTest(5)
    recordNbr := startRcdNbr + 2
    endPosition := flash.seek(handle, RECORD_LEN * 2, flash.SK_FILE_START)
    utils.evaluateSubValue(endPosition, @"seek()", RECORD_LEN * 2)
    status := flash.error()
    utils.evaluateSubStatus(handle, @"seek()", flash.SUCCESS)
    rdLength := flash.read(handle, @record, RECORD_LEN)
    ifnot verifyRecord(@record, RECORD_LEN, recordNbr, rdLength)
        ' IF WE HAVE an error then show buffers!
        dumpBlocksAtSplice(pFilename, recordNbr - startRcdNbr, totalRecordsWritten, @record, RECORD_LEN)

   ' seek to zero
    utils.startTest(@"seek to zero then read expected RECORD")
    recordNbr := startRcdNbr
    endPosition := flash.seek(handle, 0, flash.SK_FILE_START)
    utils.evaluateSubValue(endPosition, @"seek()", 0)
    status := flash.error()
    utils.evaluateSubStatus(handle, @"seek()", flash.SUCCESS)
    rdLength := flash.read(handle, @record, RECORD_LEN)
    ifnot verifyRecord(@record, RECORD_LEN, recordNbr, rdLength)
        ' IF WE HAVE an error then show buffers!
        dumpBlocksAtSplice(pFilename, recordNbr - startRcdNbr, totalRecordsWritten, @record, RECORD_LEN)

   ' seek to n-1
    utils.startTest(@"seek to N-1 then read expected RECORD")
    recordNbr := totalRecordsWritten
    endPosition := flash.seek(handle, (totalRecordsWritten - startRcdNbr) * RECORD_LEN, flash.SK_FILE_START)
    utils.evaluateSubValue(endPosition, @"seek()", (totalRecordsWritten - startRcdNbr) * RECORD_LEN)
    status := flash.error()
    utils.evaluateSubStatus(handle, @"seek()", flash.SUCCESS)
    rdLength := flash.read(handle, @record, RECORD_LEN)
    ifnot verifyRecord(@record, RECORD_LEN, recordNbr, rdLength)
        ' IF WE HAVE an error then show buffers!
        dumpBlocksAtSplice(pFilename, recordNbr - startRcdNbr, totalRecordsWritten, @record, RECORD_LEN)

    '  read past end of file to verify read returns error
    utils.startTest(@"BAD: read one more Record than written")
    utils.setCheckCountPerTest(2)
    rdLength := flash.read(handle, @record, RECORD_LEN)
    utils.evaluateSubValue(rdLength, @"read()", 0)
    status := flash.error()
    utils.evaluateSubStatus(status, @"read()", flash.E_END_OF_FILE)

    '  close file
    utils.startTest(@"Close read file")
    status := flash.close(handle)
    utils.evaluateResultNegError(status, flash.SUCCESS)


PRI testVerifyRecordsWDump(pFilename, totalRecordsWritten) | recordCount, lenBytes, startRcdNbr, recordInCircular

    '  get total file size
    utils.startTest(@"Determine size of file in records")
    lenBytes := flash.file_size(pFilename)
    status := flash.error()
    utils.evaluateResultNegError(handle, flash.SUCCESS)

    recordCount := lenBytes / RECORD_LEN                ' ignore fraction at front of file

    debug(" ")
    debug("****  At Verify: file size in bytes = ", udec_(lenBytes), " bytes, (", udec_(recordCount), " records), wrote total of ", udec_(totalRecordsWritten), " records")

    recordInCircular := (CIRCULAR_LEN_IN_BYTES + (RECORD_LEN-1)) / RECORD_LEN
    debug("   Circular Buffer contains = ", udec_(recordInCircular), " records")

    ' read should start at tail - file limit (8KB in our tests)
    if totalRecordsWritten > recordInCircular
        startRcdNbr := totalRecordsWritten - recordInCircular + 1
    else
        startRcdNbr := 1
    debug("   Read should start with record #", udec_(startRcdNbr))

    '  open file for read
    utils.startTest(@"Open File for read")
    handle := flash.open_circular(pFilename, "r", CIRCULAR_LEN_IN_BYTES)
    utils.evaluateResultNegError(handle, flash.SUCCESS)
    '    seek to various locations and read longs validating for correct value

    utils.startTest(@"read expected RECORDs Dump")
    utils.setCheckCountPerTest(3)
    recordNbr := startRcdNbr
    rdLength := flash.read(handle, @record, RECORD_LEN)
    status := flash.error()
    ' ensure length is same as expected
    utils.evaluateSubValue(rdLength, @"read()", RECORD_LEN)
    ' ensure record ID is same as expected
    utils.evaluateSubValue(recordID, @"recordID", recordNbr)
    ' ensure record CRC is same as expected
    calcCRC := utils.record_crc(@record, RECORD_LEN)
    utils.evaluateSubValue(calcCRC, @"recordCRC", recordCRC)

     ' dump record
    utils.dbgMemDump(@"Record we read (128 Byte)", @record, 128)
   ' AUGH dump head block to see if content is correct
    pBlock := flash.TEST_getHead4kBlock(pFilename)
    ' dump head block
    utils.dbgMemDump(@"Records file Head block", pBlock, 256)
    status := flash.SUCCESS ' clear our temp use

    debug("* DONE halt for eval")
    repeat



PRI verifyRecord(pRcd, rcdSize, expectedId, readLength) : bStatus
    ' ensure length is same as expected
    Debug("   -- Verifying Rcd # ", udec_(expectedId))
    bStatus := false
    utils.evaluateSubValue(readLength, @"read()", rcdSize)
    bStatus |= readLength <> rcdSize ? true : false
    ' ensure record ID is same as expected
    utils.evaluateSubValue(recordID, @"recordID", expectedId)
    bStatus |= recordID <> expectedId ? true : false
    ' ensure record CRC is same as expected
    calcCRC := utils.record_crc(@record, rcdSize)
    utils.evaluateSubValue(calcCRC, @"recordCRC", recordCRC)
    bStatus |= calcCRC <> recordCRC ? true : false

con { license }

{{
  =================================================================================================

  Terms of Use: MIT License

  Copyright (c) 2023 Iron Sheep Productions, LLC

  Permission is hereby granted, free of charge, to any person obtaining a copy of this
  software and associated documentation files (the "Software"), to deal in the Software
  without restriction, including without limitation the rights to use, copy, modify,
  merge, publish, distribute, sublicense, and/or sell copies of the Software, and to
  permit persons to whom the Software is furnished to do so, subject to the following
  conditions:

  The above copyright notice and this permission notice shall be included in all copies
  or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
  INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
  PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
  CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
  OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

  =================================================================================================
}}
